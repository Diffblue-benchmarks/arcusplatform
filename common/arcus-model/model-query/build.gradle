/*
 * Copyright 2019 Arcus Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// project specific build settings, dependencies, etc.

configurations {
    antlr4 { description "Classpath for creating Antl4 grammars" }
}

dependencies {
    compile project(':common:arcus-model:platform-messages')
    compile libraries.antlr4_runtime
    compile libraries.guava

    antlr4  libraries.antlr4
}

task antlrOutputDir {
       doLast {
        mkdir(generateSource.destinationDir)
    }
}


task generateSource(dependsOn: antlrOutputDir, type: JavaExec) {
    description = 'Generates Java sources from Antlr4 grammars.'

    ext.generatedPackage = 'com.iris.model.query.antlr'
    ext.destinationDir = "$buildDir/generated-src/antlr"
    ext.sourceDir = 'src/main/antlr'

    inputs.dir file(sourceDir)
    outputs.dir file(destinationDir)

    def grammars = fileTree(sourceDir).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4

    def files = []
    grammars.visit { if(!it.directory) files << it.path }
    workingDir sourceDir
    args = ['-o', destinationDir] + files
}

task run(dependsOn: compileJava, type: JavaExec) {
    main = 'com.iris.model.query.Main'
    classpath = sourceSets.main.runtimeClasspath
}

compileJava {
    dependsOn generateSource
    source generateSource.destinationDir
}

eclipseClasspath.dependsOn generateSource

sourceSets {
    main {
        java {
            srcDir generateSource.destinationDir
        }
    }

    // load it up as a source folder in eclipse,
    // but don't include in the jar
    antlr4 {
       resources {
          srcDir generateSource.sourceDir
       }
    }
}


